
#Область ПрограммныйИнтерфейс

// Зависимости:
//   ТекущаяОбласть - ПрофильПочтыМассаднраРУ, ПолучитьПароль
//
// Параметры:
// 	ОписаниеПисьма - Структура:
//		* ОтправительИмя - Строка - имя (адрес) отправителя письма (app1C@massandra.ru)
//		* Получатели - Массив Из Строка - адреса разделенные запятой (it@massandra.su, it@massandra.ru)
//		* Тема - Строка - тема письма
//		* Текст - Строка - текст в тело письма
//		* ТипТекста - ТипТекстаПочтовогоСообщения - Тип текстовых данных письма
//		* ФайлыВложения - Любой файл (бинарные данные)
//
// Возвращаемое значение:
//   Булево - Истина, если письмо успешно отправленно
//
Функция ОтправитьПочтовоеСообщение(ОписаниеПисьма) Экспорт

	Если ОписаниеПисьма.Получатели.Количество() = 0 Тогда
		Сообщить("Укажите получателя письма!");
		Возврат Ложь;
	КонецЕсли;
	
	logApp1C = ОписаниеПисьма.ПользовательSMTP;
	pasApp1C = ПолучитьПароль(logApp1C);
	Профиль  = ПрофильПочтыМассаднраРУ(logApp1C, pasApp1C);
	Письмо 	 = Новый ИнтернетПочтовоеСообщение;	
	Текст 	 = Письмо.Тексты.Добавить(ОписаниеПисьма.Текст, ?(ОписаниеПисьма.ТипТекста = Неопределено, 
									ТипТекстаПочтовогоСообщения.ПростойТекст, ОписаниеПисьма.ТипТекста) );
	
	ИтераторВложений = 0;
	Для Каждого Файл Из ОписаниеПисьма.ФайлыВложения Цикл
		Если ЗначениеЗаполнено(Файл.Путь) И ЗначениеЗаполнено(Файл.Имя) Тогда
			ДвоичныеДанные = Новый ДвоичныеДанные(Файл.ПолныйПуть);			
						
			Письмо.Вложения.Добавить(ДвоичныеДанные, Файл.Имя);
			Письмо.Вложения[ИтераторВложений].ТипСодержимого = Файл.ТипMIME;
			Письмо.Вложения[ИтераторВложений].Кодировка = "utf-8";
				
			ИтераторВложений = ИтераторВложений + 1;
		КонецЕсли;
	КонецЦикла;
	
	Письмо.Тема 			= ОписаниеПисьма.Тема; 
	Письмо.Отправитель 		= профиль.Пользователь;
	Письмо.ИмяОтправителя 	= ОписаниеПисьма.ОтправительИмя;
	
	ИтераторПолучателей = 0;
	Для Каждого получатель ИЗ ОписаниеПисьма.Получатели Цикл
		Если ИтераторПолучателей > 0 Тогда
			Письмо.Копии.Добавить(получатель);
		Иначе
			Письмо.Получатели.Добавить(получатель) ;
		КонецЕсли;
		ИтераторПолучателей = ИтераторПолучателей + 1;
	КонецЦикла;
	
	Письмо.СпособКодированияНеASCIIСимволов = СпособКодированияНеASCIIСимволовИнтернетПочтовогоСообщения.MIME;
	
	Почта = Новый ИнтернетПочта;	
	Попытка
		Почта.Подключиться(Профиль);
		Почта.Послать(Письмо, ОбработкаТекстаИнтернетПочтовогоСообщения.Обрабатывать, ПротоколИнтернетПочты.SMTP);
	Исключение
		Возврат ЛОЖЬ;
	КонецПопытки;
	
	Почта.Отключиться();
	
	Возврат ИСТИНА;
	
КонецФункции

// Функция - Новый описание интернет почта письма
// 
// Возвращаемое значение:
//  ОписаниеПисьма - Структура:
//		* ПользовательSMTP - Строка - ИнтернетПочтовыйПрофиль.Пользователь
//		* ОтправительИмя - Строка - имя (адрес) отправителя письма (app1C@massandra.ru)
//		* Получатели - Массив Из Строка - адреса разделенные запятой (it@massandra.su, it@massandra.ru)
//		* Тема - Строка - тема письма
//		* Текст - Строка - текст в тело письма
//		* ТипТекста - ТипТекстаПочтовогоСообщения - Тип текстовых данных письма
//		* ФайлыВложения - Любой файл (бинарные данные)
//
Функция НовыйОписаниеИнтернетПочтаПисьма() Экспорт
	
	ФайлыВложения 	= Новый Массив;
	ОписаниеПисьма 	= Новый Структура;
	
	ОписаниеПисьма.Вставить("ПользовательSMTP");
	ОписаниеПисьма.Вставить("ОтправительИмя");
	ОписаниеПисьма.Вставить("Получатели",		Новый Массив);	
	ОписаниеПисьма.Вставить("Тема");
	ОписаниеПисьма.Вставить("Текст");
	ОписаниеПисьма.Вставить("ТипТекста",		Неопределено);
	ОписаниеПисьма.Вставить("ФайлыВложения",	ФайлыВложения);
	
	Возврат ОписаниеПисьма;

КонецФункции // НовыйОписаниеИнтернетПочтаПисьма()

// Функция - Новый файл вложения
//
// Параметры:
// 	Путь - Строка - абсолютный путь к файлу
//  Имя - Строка - Имя файла
//  Расширение - Строка - DOCX, HTML, ODS, PDF, TXT, XLS, XLSX
// 
// Возвращаемое значение:
//  Вложение - Структура
//
Функция НовыйФайлВложения(Путь, Имя, расширениеФайла) Экспорт

	CommonMIMEtypes = Новый Соответствие;
	
	CommonMIMEtypes.Вставить("DOCX", 	"application/vnd.openxmlformats-officedocument.wordprocessingml.document");
	CommonMIMEtypes.Вставить("HTML", 	"text/html");
	CommonMIMEtypes.Вставить("ODS", 	"application/vnd.oasis.opendocument.spreadsheet");
	CommonMIMEtypes.Вставить("PDF", 	"application/pdf");
	CommonMIMEtypes.Вставить("TXT", 	"text/plain");
	CommonMIMEtypes.Вставить("XLS", 	"application/vnd.ms-excel");
	CommonMIMEtypes.Вставить("XLSX", 	"application/vnd.ms-excel");
		
	MIMEtypes 	= CommonMIMEtypes.Получить(расширениеФайла);
	MIMEtypes 	= ?(MIMEtypes = Неопределено, "", MIMEtypes);
	
	ПолныйПуть 	= Путь + Имя;
	Вложение 	= Новый Структура;
	
	Вложение.Вставить("ПолныйПуть", 	ПолныйПуть);
	Вложение.Вставить("Путь",			Путь);
	Вложение.Вставить("Имя",			Имя);
	Вложение.Вставить("ТипMIME", 		MIMEtypes);
	Вложение.Вставить("Расширение", 	расширениеФайла);
	
	Возврат Вложение;

КонецФункции // НовыйФайлВложения()

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КонфигПочтовогоКлиента

// Функция - Профиль почты массаднра РУ
//
// Параметры:
//  Пользователь - Строка - ИнтернетПочтовыйПрофиль.Пользователь
//  Пароль		 - Строка - ИнтернетПочтовыйПрофиль.Пароль
// 
// Возвращаемое значение:
//  профиль - ИнтернетПочтовыйПрофиль
//
Функция ПрофильПочтыМассаднраРУ(Пользователь, Пароль) Экспорт

	профиль = Новый ИнтернетПочтовыйПрофиль;
	
	профиль.АдресСервераSMTP 	= "mail.massandra.ru";
    профиль.ПортSMTP 			= 465;
	профиль.Пользователь 		= Пользователь;
	профиль.Пароль 				= Пароль;
	профиль.ПользовательSMTP 	= Пользователь;
	профиль.ПарольSMTP 			= Пароль;	
	профиль.АутентификацияSMTP 	= СпособSMTPАутентификации.Login;

	Возврат профиль;
	
КонецФункции

// Функция - Получить пароль
//
// Параметры:
//  ИмяПользователя	 - Строка - Логин по которому будет получен пароль
// 
// Возвращаемое значение:
//  результат - Строка - пароль для запрашиваемого логина
//
Функция ПолучитьПароль(ИмяПользователя) Экспорт
	
	Соединение 		= Новый HTTPСоединение("192.168.171.150");		
	СтрокаЗапроса	= "/Honeywell/q/passport/ps-mdYA-nfkTFMqq-azpPDardapqb.php?Login=" + ИмяПользователя;	
	Запрос 			= Новый HTTPЗапрос(СтрокаЗапроса); 
	Ответ 			= Соединение.Получить(Запрос);
	результат 		= Неопределено;
	
	Если Ответ.КодСостояния = 200 Тогда
		результат = Ответ.ПолучитьТелоКакСтроку(); 	
	КонецЕсли;
	
	Возврат результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти